// Type Definitions

type ServerId : 1..3;
type ServerNum: 0..3;
type ClientId : 1..3;
type RequestId : 0..99;
type View: 1..9;

type us: 0..10000000;
type ms: 0..10000;

type NodeId : 1..3;

type Request: record {
  clientId: ClientId,
  requestId: RequestId,
  // actual request omitted
}

type Entry: record  {
  view: View,
  committed: Boolean,
  request: Request,
}
type Index: 1..10;
type Log: Vector<Entry>[Index];

type CoordinatorState : record {
  request: Request,
  prepare: ServerNum,
  commit: ServerNum,
}

type MasterState : either {
  Self {
    coord: Vector<CoordinatorState>[Index]
  },
  Other {
    masterId: ServerId,
    timeoutAt: Time,
  }
}

type Server : record {
  view: View,
  stateMachine: Array<RequestId>[ClientId],
  log: Log,
  logIndex: Index, //next free index
  commitIndex: Index,
  master: MasterState,
};

type Client : record {
  requestId: RequestId,
  masterId: ServerId,
  timeoutAt: Time,
  waiting: Boolean
}

type Payload : either {
  ClientResponse {
    requestId: RequestId,
    success: either {
      Successful,
      Unsuccessful {try: ServerId},
    }
  },
  ClientRequest {
    requestId: RequestId,
  },
  PrepareRequest {
    index: Index,
    request: Request,
  },
  PrepareResponse {
    index: Index,
    success: Boolean,
  },
  CommitRequest {
    index: Index,
    request: Request,
  },
  CommitResponse {
    index: Index,
    success: Boolean
  },
}

type Packet : record {
  sentAt: Time,
  sentBy : NodeId,
  arriveBy: Time,
  arriveAt: NodeId,
  payload: Payload,

};

// Protocol State

var servers: Array<Server>[ServerId];
var clients: Array<Client>[ClientId];
var network: Set<Packet>[0..20];

var clientTimeoutMin : us = 100000;
var clientTimeoutMax : us = 200000;
var clientRetry : us = 100000;
var netDelayMin : us = 5000;
var netDelayMax : us = 10000;
var FailureTimer : us = 30000;

var majority:ServerNum = (size(servers)/2) +1;

// Useful functions

function getMasterId(view: View) -> ServerId {
 return view % size(servers); // FIX
}

function makeClientTimeout() -> Time {
  return later(urandomRange(clientTimeoutMin, clientTimeoutMax));
}

function sendAll(from: NodeId, p: Payload) {
  for serverId, server in servers {
    if serverId != from {
      send(serverId, from, p);
    }
  }
}

function send(to: NodeId, from: NodeId, p: Payload) {
  push(network, Packet {
    sentBy: from,
    sentAt: later(0),
    arriveAt: to,
    arriveBy: later(urandomRange(netDelayMin, netDelayMax)),
    payload: p,
  });
}

// check all outstanding command to see if any can be applied
function apply(id: ServerId) {
  var index:Index = servers[id].commitIndex;
  while index < servers[id].logIndex {
    if servers[id].log[index].committed {
      servers[id].stateMachine[servers[id].log[index].request.clientId] = servers[id].log[index].request.requestId;
      servers[id].commitIndex = index;
      index += 1;
    }
    else {break;}
  }
}

function init() {
  for client in clients {
    client.timeoutAt = makeClientTimeout();
    client.masterId = 1;
    client.requestId = 1;
  }
  for serverId, server in servers {
    var masterId : ServerId = getMasterId(server.view);
    if masterId==serverId {
      server.master = Self {};
    } else {
      server.master = Other {masterId: masterId, timeoutAt: later(FailureTimer)};
    }
  }

}

// Initialization
init();


// State Transitions
rule newRequest for clientId, client in clients {
  if past(client.timeoutAt) {
    if client.waiting {
      client.masterId = client.masterId + 1;
    } else {
      client.waiting = True;
    }
    send(client.masterId, clientId, ClientRequest {requestId: client.requestId});
    client.timeoutAt = later(clientRetry);
    }
}

rule handleClientRequest for msg in network {
  if past(msg.arriveBy) {
    match msg.payload {
      ClientRequest(data) {
        match servers[msg.arriveAt].master {
          Self(masterState) {
              var request: Request = Request {clientId:msg.sentBy, requestId:data.requestId};
              var index: Index = servers[msg.arriveAt].logIndex;
              servers[msg.arriveAt].logIndex += 1;
              push(servers[msg.arriveAt].log,
                Entry{view: servers[msg.arriveAt].view, committed:False, request:request});
              push(masterState.coord,
                 CoordinatorState{request:request, prepare: 1, commit:0});
              sendAll(
                msg.arriveAt,
                PrepareRequest {index: index, request: request});
              servers[msg.arriveAt].master = Self{coord:masterState.coord};
          }
          Other(masterData) {
            send(
              msg.sentBy,
              msg.arriveAt,
              ClientResponse {requestId: data.requestId, success:Unsuccessful {try:masterData.masterId}});
          }
        }
      remove(network,msg);
    }
  default {}
  }
}
}

rule handleClientResponse for msg in network {
  if past(msg.arriveBy) {
    match msg.payload {
      ClientResponse(data) {
        if clients[msg.arriveAt].waiting &&
            data.requestId==clients[msg.arriveAt].requestId {
              match data.success {
                Successful {
                  clients[msg.arriveAt].requestId = clients[msg.arriveAt].requestId + 1;
                  clients[msg.arriveAt].waiting = False;
                  clients[msg.arriveAt].timeoutAt = makeClientTimeout();
                }
                Unsuccessful(hint) {
                  clients[msg.arriveAt].masterId = hint.try;
                  send(clients[msg.arriveAt].masterId, msg.arriveAt, ClientRequest {requestId: data.requestId});
                  clients[msg.arriveAt].timeoutAt = later(clientRetry);
                }
              }
            }
        remove(network,msg);
      }
    default {}
    }
  }
}

rule handlePrepareRequest for msg in network {
  if past(msg.arriveBy) {
    match msg.payload {
      PrepareRequest(data) {
        push(servers[msg.arriveAt].log, Entry{request:data.request,committed:False});
        servers[msg.arriveAt].logIndex += 1;
        send(
          msg.sentBy,
          msg.arriveAt,
          PrepareResponse {index: data.index, success:True});
        remove(network,msg);
        }
    default {}
    }
  }
}

rule handlePrepareResponse for msg in network {
  if past(msg.arriveBy) {
    match msg.payload {
      PrepareResponse(data) {
        match servers[msg.arriveAt].master {
          Self(state) {
            state.coord[data.index].prepare += 1;
            if state.coord[data.index].prepare==majority {
              var request:Request = state.coord[data.index].request;
              var entry:Entry = Entry {view: servers[msg.arriveAt].view, committed:True, request:request};
              servers[msg.arriveAt].log[data.index] = entry;
              apply(msg.arriveAt);
              send(
                msg.sentBy,
                entry.request.clientId,
                ClientResponse {requestId: request.requestId, success: Successful});
              sendAll(
                msg.arriveAt,
                CommitRequest{index:data.index,request:request});
            }
          servers[msg.arriveAt].master = Self{coord:state.coord};

          }
          Other(masterData) {} // ignore
        }
        remove(network,msg);
      }
    default {}
    }
  }
}

rule handleCommitRequest for msg in network {
  if past(msg.arriveBy) {
    match msg.payload {
      CommitRequest(data) {
        servers[msg.arriveAt].log[data.index] =
          Entry {view: servers[msg.arriveAt].view,committed:True, request:data.request};
        apply(msg.arriveAt);
        remove(network,msg);
      }
    default {}
    }
  }
}

rule handleCommitResponse for msg in network {
  if past(msg.arriveBy) {
    match msg.payload {
      CommitResponse(data) {
        remove(network,msg);
      }
    default {}
    }
  }
}
