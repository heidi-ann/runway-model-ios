// Type Definitions

type ServerId : 1..5;
type ServerNum: 0..5;
type ClientId : 1..2;
type RequestId : 0..99;
type View: 1..9;

type us: 0..10000000;
type ms: 0..10000;

type NodeId : 1..5;

type Request: either {
  Empty {},
  Occupied {
    clientId: ClientId,
    requestId: RequestId,
    // actual request omitted
  },
}

type Entry: record  {
  view: View,
  committed: Boolean,
  request: Request,
}

type Index: 1..10;
type Log: Vector<Entry>[Index];

type CoordinatorState : record {
  index: Index,
  prepare: ServerNum,
  commit: ServerNum,
}

type RecoveryState : record {
  index: Index,
  candidate: Request,
}

type Watcher: record {
  index: Index,
  assignedTo: ServerId,
  timeoutAt: Time,
}

type MasterState : either {
  Unknown {},
  Self {
    coordinators: Set<Watcher>[Index],
    nextHeartbeat: Time,
    },
  Other {
    masterId: ServerId,
    timeoutAt: Time,
  }
}

type ServerMem: record {
  logIndex: Index, //next free index
  commitIndex: Index,
  stateMachine: Array<RequestId>[ClientId],
  master: MasterState,
  coordinator: Vector<CoordinatorState>[Index],
  recoveryCoordinator: Vector<RecoveryState>[Index],
}

type ServerDisk : record {
  view: View,
  log: Log,
};

type Client : record {
  requestId: RequestId,
  masterId: ServerId,
  timeoutAt: Time,
  waiting: Boolean
}

type Payload : either {
  ClientResponse {
    requestId: RequestId,
    success: either {
      Successful,
      Unsuccessful {try: ServerId},
    }
  },
  ClientRequest {
    requestId: RequestId,
  },
  PrepareRequest {
    view: View,
    index: Index,
    request: Request,
  },
  PrepareResponse {
    index: Index,
    success: Boolean,
  },
  CommitRequest {
    view: View,
    index: Index,
    request: Request,
  },
  CommitResponse {
    index: Index,
    success: Boolean,
  },
  CoordinateRequest {
    view: View,
    index: Index,
    request: Request,
  },
  CoordinateResponse {
    success: Boolean,
  },
  RecoveryRequest {
    view: View,
    index: Index,
    request: Request,
  },
  RecoveryResponse {
    index: Index,
    request: Request,
  },
  QueryRequest {
    view: View,
    index: Index,
  },
  QueryResponse {
    index: Index,
    occupied: Boolean,
    request: Request,
  },
  Heartbeat,
}

type Packet : record {
  sentAt: Time,
  sentBy : NodeId,
  arriveBy: Time,
  arriveAt: NodeId,
  payload: Payload,

};

type Status: either {
  Online,
  Offline,
};

// Protocol State

var serverStatus: Array<Status>[ServerId];
var serversDisk: Array<ServerDisk>[ServerId];
var serversMem: Array<ServerMem>[ServerId];
var clients: Array<Client>[ClientId];
var network: Set<Packet>[0..20];

var clientTimeoutMin : us = 100000;
var clientTimeoutMax : us = 200000;
var clientRetry : us = 100000;
var netDelayMin : us = 5000;
var netDelayMax : us = 10000;
var failureTimer : us = 30000;
var heartbeatTimer: us = 10000;

var majority:ServerNum = (size(serversMem)/2) +1;

// Useful functions

function getMasterId(view: View) -> ServerId {
 return view % size(serversMem); // FIX
}

function makeClientTimeout() -> Time {
  return later(urandomRange(clientTimeoutMin, clientTimeoutMax));
}

function sendAll(from: NodeId, p: Payload) {
  for serverId, server in serversMem {
    if serverId != from {
      send(serverId, from, p);
    }
  }
}

function sendAllBut(from: NodeId, but: NodeId, p: Payload) {
  for serverId, server in serversMem {
    if serverId != from && serverId != but{
      send(serverId, from, p);
    }
  }
}

function send(to: NodeId, from: NodeId, p: Payload) {
  push(network, Packet {
    sentBy: from,
    sentAt: later(0),
    arriveAt: to,
    arriveBy: later(urandomRange(netDelayMin, netDelayMax)),
    payload: p,
  });
}

function ready(msg: Packet) -> Boolean {
  if past(msg.arriveBy) {
    remove(network,msg);
    return (serverStatus[msg.arriveAt]==Online);
  }
  return False;
}

function init() {
  for client in clients {
    client.masterId = 1;
    client.requestId = 1;
  }
  for id, server in serversDisk {
    var masterId : ServerId = getMasterId(server.view);
    if masterId==id {
      serversMem[id].master = Self {};
    } else {
      serversMem[id].master = Other {masterId: masterId, timeoutAt: later(failureTimer)};
    }
  }

}

function stop(id: ServerId) {
  var cleanServer: ServerMem;
  serversMem[id] = cleanServer;
}

function restart(id: ServerId) {
  serversMem[id].master = Other {masterId: getMasterId(serversDisk[id].view), timeoutAt: later(failureTimer)};
  serversMem[id].logIndex = size(serversDisk[id].log)+1;
}

function updateView(id: ServerId, view: View) {
  if view > serversDisk[id].view {
    serversDisk[id].view = view;
    var nextMaster: ServerId = getMasterId(view);
    if nextMaster == id {
      // this node is the next master
      serversMem[id].master = Self {};
    } else {
      // wait for the expected new master
      serversMem[id].master = Other{
        masterId:nextMaster,
        timeoutAt: later(failureTimer)};
    }
    var nill: Vector<CoordinatorState>[Index];
    serversMem[id].coordinator = nill;
  }
}

// Initialization
init();


// Internal state Transitions

rule coordinatorFailed {
  for id, server in serversMem {
      match server.master {
        Self(state) {
          for watcher in state.coordinators {
            if past(watcher.timeoutAt) {
              if !serversDisk[id].log[watcher.index].committed {
                send(watcher.assignedTo+1, id,
                  CoordinateRequest {
                    index: watcher.index,
                    request: serversDisk[id].log[watcher.index].request});
                push(state.coordinators,
                  Watcher{
                    index: watcher.index,
                    assignedTo: watcher.assignedTo+1,
                    timeoutAt: later(failureTimer)});
              }
              remove(state.coordinators, watcher);
              serversMem[id].master = Self{coordinators:state.coordinators};
              }
            }
          }
      default {}
      }
    }
}

rule MasterFailed {
  for id, server in serversMem {
      match server.master {
        Other(state) {
            if past(state.timeoutAt) {
              updateView(id, serversDisk[id].view + 1);
            }
        }
        default {}
    }
  }
}

external newRequest for clientId, client in clients {
  if !client.waiting && client.requestId<=3 {
    send(client.masterId, clientId, ClientRequest {requestId: client.requestId});
    client.waiting = True;
    client.timeoutAt = later(clientRetry);
  }
}

rule retryRequest for clientId, client in clients {
  if client.waiting && past(client.timeoutAt) {
    client.masterId = client.masterId + 1;
    send(client.masterId, clientId, ClientRequest {requestId: client.requestId});
    client.timeoutAt = later(clientRetry);
    }
}

rule dispatchHeartbeat for id, server in serversMem {
  match server.master {
    Self(state) {
      if past(state.nextHeartbeat) {
        sendAll(id,Heartbeat);
        serversMem[id].master = Self{nextHeartbeat:later(heartbeatTimer)};
      }
    }
    default {}
  }
}

rule handleHeartbeat for msg in network {
  if past(msg.arriveBy) && serverStatus[msg.arriveAt]==Online {
    match msg.payload {
      Heartbeat {
        match serversMem[msg.arriveAt].master {
          Other(state) {
            if state.masterId==msg.sentBy {
              serversMem[msg.arriveAt].master = Other{
                masterId: state.masterId,
                timeoutAt:later(failureTimer)};
            }
          }
          default {}
          }
        remove(network,msg);
      }
    default {}
    }
  }
}

rule handleClientRequest for msg in network {
  if past(msg.arriveBy) && serverStatus[msg.arriveAt]==Online {
    match msg.payload {
      ClientRequest(data) {
        match serversMem[msg.arriveAt].master {
          Self(masterState) {
              // replicate locally
              var request: Request = Occupied{clientId:msg.sentBy, requestId:data.requestId};
              var index: Index = serversMem[msg.arriveAt].logIndex;
              serversMem[msg.arriveAt].logIndex += 1;
              push(serversDisk[msg.arriveAt].log,
                Entry{view: serversDisk[msg.arriveAt].view, committed:False, request:request});
              // forward to coordinator
              send(
                msg.arriveAt+1,
                msg.arriveAt,
                CoordinateRequest {index: index, request: request});
              // timeout to detect failed coordinator
              push(masterState.coordinators,
                Watcher{
                  index: index,
                  assignedTo: msg.arriveAt+1,
                  timeoutAt: later(failureTimer),
                });
              serversMem[msg.arriveAt].master = Self{coordinators:masterState.coordinators};
          }
          Other(masterData) {
            send(
              msg.sentBy,
              msg.arriveAt,
              ClientResponse {requestId: data.requestId, success:Unsuccessful {try:masterData.masterId}});
          }
          default {}
        }
      remove(network,msg);
    }
  default {}
  }
}
}

rule handleClientResponse for msg in network {
  if past(msg.arriveBy) && serverStatus[msg.arriveAt]==Online {
    match msg.payload {
      ClientResponse(data) {
        if clients[msg.arriveAt].waiting &&
            data.requestId==clients[msg.arriveAt].requestId {
              match data.success {
                Successful {
                  clients[msg.arriveAt].requestId = clients[msg.arriveAt].requestId + 1;
                  clients[msg.arriveAt].waiting = False;
                }
                Unsuccessful(hint) {
                  clients[msg.arriveAt].masterId = hint.try;
                  send(clients[msg.arriveAt].masterId, msg.arriveAt, ClientRequest {requestId: data.requestId});
                  clients[msg.arriveAt].timeoutAt = later(clientRetry);
                }
              }
            }
        remove(network,msg);
      }
    default {}
    }
  }
}

rule handlePrepareRequest for msg in network {
  if past(msg.arriveBy) && serverStatus[msg.arriveAt]==Online {
    match msg.payload {
      PrepareRequest(data) {
        updateView(msg.arriveAt,data.view);
        push(serversDisk[msg.arriveAt].log, Entry{request:data.request,committed:False});
        serversMem[msg.arriveAt].logIndex += 1;
        send(
          msg.sentBy,
          msg.arriveAt,
          PrepareResponse {index: data.index, success:True});
      remove(network,msg);
        }
    default {}
    }
  }
}

rule handlePrepareResponse for msg in network {
  if past(msg.arriveBy) && serverStatus[msg.arriveAt]==Online {
    match msg.payload {
      PrepareResponse(data) {
        for coordState in serversMem[msg.arriveAt].coordinator {
          if data.index == coordState.index {
            coordState.prepare += 1;
            if coordState.prepare==majority {
              var request:Request = serversDisk[msg.arriveAt].log[data.index].request;
              var entry:Entry = Entry {view: serversDisk[msg.arriveAt].view, committed:True, request:request};
              serversDisk[msg.arriveAt].log[data.index] = entry;
              // check for outstanding entries
              var index:Index = serversMem[msg.arriveAt].commitIndex;
              while index < serversMem[msg.arriveAt].logIndex {
                if serversDisk[msg.arriveAt].log[index].committed {
                  match serversDisk[msg.arriveAt].log[index].request {
                    Occupied(req) {
                      serversMem[msg.arriveAt].stateMachine[req.clientId] = req.requestId;
                      send(
                        req.clientId,
                        msg.arriveAt,
                        ClientResponse {requestId: req.requestId, success: Successful});
                    }
                    Empty {}
                  }
                  serversMem[msg.arriveAt].commitIndex = index;
                  index += 1;
                }
                else {break;}
              }
              coordState.commit += 1;
              sendAll(
                msg.arriveAt,
                CommitRequest{index:data.index,request:request});
            }
          }
        }
      remove(network,msg);
    }
    default {}
    }
  }
}

rule handleCommitRequest for msg in network {
  if past(msg.arriveBy) && serverStatus[msg.arriveAt]==Online {
    match msg.payload {
      CommitRequest(data) {
        updateView(msg.arriveAt,data.view);
        serversDisk[msg.arriveAt].log[data.index] =
          Entry {view: serversDisk[msg.arriveAt].view,committed:True, request:data.request};
        var index:Index = serversMem[msg.arriveAt].commitIndex;
        while index < serversMem[msg.arriveAt].logIndex {
          if serversDisk[msg.arriveAt].log[index].committed {
            match serversDisk[msg.arriveAt].log[index].request {
              Occupied(req) {
                serversMem[msg.arriveAt].stateMachine[req.clientId] = req.requestId;
              }
              Empty {}
            }
            serversMem[msg.arriveAt].commitIndex = index;
            index += 1;
          }
          else {break;}
        }
        send(
          msg.sentBy,
          msg.arriveAt,
          CommitResponse {index: data.index, success:(serversMem[msg.arriveAt].commitIndex>=data.index) });
        remove(network,msg);
      }
    default {}
    }
  }
}

rule handleCommitResponse for msg in network {
  if past(msg.arriveBy) && serverStatus[msg.arriveAt]==Online {
    match msg.payload {
      CommitResponse(data) {
        for coordState in serversMem[msg.arriveAt].coordinator {
          if data.index == coordState.index {
            coordState.commit += 1;
            if coordState.commit == size(serversMem) {
              remove(serversMem[msg.arriveAt].coordinator,coordState);
            }
          }
        }
        remove(network,msg);
      }
    default {}
    }
  }
}

rule handleCoordinateRequest for msg in network {
  if past(msg.arriveBy) && serverStatus[msg.arriveAt]==Online {
    match msg.payload {
      CoordinateRequest(data) {
        updateView(msg.arriveAt,data.view);
        // replicate locally
        serversMem[msg.arriveAt].logIndex += 1; // TODO: FIX ASSUMPTION
        push(serversDisk[msg.arriveAt].log,
          Entry{view: serversDisk[msg.arriveAt].view, committed:False, request:data.request});
        // start prepare phase
        push(serversMem[msg.arriveAt].coordinator,
           CoordinatorState{index:data.index, prepare: 2, commit:0});
        sendAllBut(
          msg.arriveAt,
          msg.sentBy,
          PrepareRequest {view: serversDisk[msg.arriveAt].view,index: data.index, request: data.request});
        // reply to master
        send(
          msg.sentBy,
          msg.arriveAt,
          CoordinateResponse {success:True});
        remove(network,msg);
      }
    default {}
    }
  }
}

rule handleCoordinateResponse for msg in network {
  if past(msg.arriveBy) && serverStatus[msg.arriveAt]==Online {
    match msg.payload {
      CoordinateResponse {
        // do nothing
        remove(network,msg);
      }
    default {}
    }
  }
}

// TODO: test if we can remove online from all rules by making this rule first
rule disgardMsgs for msg in network {
  if past(msg.arriveBy) && serverStatus[msg.arriveAt]==Offline {
    remove(network,msg);
  }
}

// External state Transitions

external nodeFailure for id, status in serverStatus {
  status = Offline;
  stop(id);
}

external nodeRecovery for id, status in serverStatus {
  status = Online;
  restart(id);
}
